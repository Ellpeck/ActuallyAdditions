buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'idea'

version = "1.14.4-r151"
group = "de.ellpeck.actuallyadditions"
archivesBaseName = "ActuallyAdditions"

if(hasProperty('buildnumber')){
    version = "${version}-${this.properties['buildnumber']}"
}

minecraft {
    mappings channel: "snapshot", version: "20191107-1.14.3"
	runs {
        client = {
            workingDirectory project.file("run/client").canonicalPath
            mods {
                actuallyadditions {
                    source sourceSets.main
				}
			}
        }
        server = {
            workingDirectory project.file("run/server").canonicalPath
            mods {
                actuallyadditions {
                    source sourceSets.main
				}
			}
        }
        data = {
            workingDirectory project.file("run/data").canonicalPath
            args "--mod", "actuallyadditions", "--all", "--output", "\"" + file("src/generated/resources").toString() + "\"", "--validate", "--existing", "\"" + sourceSets.main.resources.srcDirs[0] + "\""
            mods {
                actuallyadditions {
                    source sourceSets.main
				}
			}
        }
    }
}

repositories {
    maven {
        url "https://dvs1.progwml6.com/files/maven"
    }
	maven {
		url "http://tehnut.info/maven"
	}
	maven {
        url "https://dl.bintray.com/cyclopsmc/dev/"
    }
}

dependencies {
	minecraft "net.minecraftforge:forge:1.14.4-28.1.86"
	compileOnly fg.deobf("mezz.jei:jei-1.14.4:6.0.0.24:api")
  	runtimeOnly fg.deobf("mezz.jei:jei-1.14.4:6.0.0.24")
	//deobfCompile "mcp.mobius.waila:Hwyla:1.8+"
    //deobfCompile ("org.cyclops.commoncapabilities:CommonCapabilities:1.12.2-+")
}

def normalManifest = {
	attributes(
		"Built-On": "1.14.4",
		"Implementation-Title": "ActuallyAdditions",
    	"Implementation-Version": project.version,
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
	)
}

jar {
	exclude(".cache")
   	manifest normalManifest
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.java
    classifier = 'dev'
    
    exclude(".cache")
   	manifest normalManifest
}

task apiJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.java
    classifier = 'api'
    include 'de/ellpeck/actuallyadditions/api/**'
}

javadoc {
    include 'de/ellpeck/actuallyadditions/api/**'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    classifier 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java
    classifier = 'sources'
}

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact apiJar
            artifact javadocJar
            artifact sourcesJar
            artifact deobfJar
        }
    }
    repositories {
        maven {
            url "file:///srv/nginx/maven"
        }
    }
}